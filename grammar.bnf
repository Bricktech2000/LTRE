<regex> ::= "!"? <term> (("|" | "&" | "=") <regex>)? # a regex is alternations, intersections and biconditionals of possibly complemented terms
<term> ::= <factor>* (":" <term>)? # a term is dual concatenations of concatenations of factors
<factor> ::= <atom> (":"? ("*" | "+" | "?" | "{" <natural>? ("," <natural>?)? "}"))* ("!" <factor>)? # a factor is a quantified atom
<atom> ::= "%" | <symset> | "(" <regex> ")" # an atom is a wildcard, a symset, or a subregex
<symset> ::= "~"? ("." | "\\" <shorthand> | <symbol> ("-" <symbol>)? | "[" <symset>* "]" | "<" <symset>* ">") # a symset is a set of symbols
<shorthand> ::= "d" | "D" | "s" | "S" | "w" | "W" # a shorthand is a predefined symset
<symbol> ::= "\\" <escape> | (? `isprint` except <metachar> ?) # a symbol matches a single character
<escape> ::= "a" | "b" | "f" | "n" | "r" | "t" | "v" | "x" <hexbyte> | <metachar> # an escape is a symbol alias
<hexbyte> ::= (? `isxdigit` ?){2}
<metachar> ::= "\\" | "-" | "." | "~" | "[" | "]" | "<" | ">" | "%" | "{" | "}" | "*" | "+" | "?" | ":" | "|" | "&" | "=" | "!" | "(" | ")"
<natural> ::= (? `isdigit` ?)+
